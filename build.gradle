plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1" apply false
}

group = 'ua.beengoo.logdo2'
version = 'devbuild'

def forcedVer = findProperty('releaseVersion') ?: System.getenv('RELEASE_VERSION')
if (forcedVer) {
    version = forcedVer
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven {
            name = "papermc-repo"
            url = "https://repo.papermc.io/repository/maven-public/"
        }
    }

    dependencies {
        compileOnly("org.projectlombok:lombok:1.18.42")
        annotationProcessor("org.projectlombok:lombok:1.18.42")
    }

    def targetJavaVersion = 21
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        if (JavaVersion.current().isJava10Compatible()) {
            options.release.set(21)
        }
    }

    test { useJUnitPlatform() }

    tasks.withType(Jar).configureEach {
        manifest {
            attributes(
                    'Implementation-Title': project.name,
                    'Implementation-Version': project.version
            )
        }
    }
}
